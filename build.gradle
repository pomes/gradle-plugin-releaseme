plugins {
    id 'idea'
    id 'eclipse'
}

wrapper.gradleVersion = '2.14.1'
group = 'com.github.pomes.gradle-plugins'
version = '0.0.1-SNAPSHOT'

subprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'groovy'
    apply plugin: 'java-gradle-plugin'
    apply plugin: 'maven-publish'

    version = rootProject.version
    group = rootProject.group

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.4.4'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':distextra') {
    description = 'Prepares various extra items for a distribution'
}

project(':gitbase') {
    description = 'Small plugin that connects to the local git repo and GitHub API for the repo'
    dependencies {
        compile project(':util')
    }
}

project(':projectinfo') {
    description = 'Grabs project information from GitHub'
    dependencies {
        compile project(':gitbase')
    }
}

project(':tagger') {
    description = 'Tags off a git repo, ready for release'
    dependencies {
        compile project(':gitbase')
    }
}

project(':releaseme') {
    description = 'An extremely opinionated plugin to help with releases'
    dependencies {
        compile project(':tagger')
        compile project(':projectinfo')
        compile 'org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0'
        compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    }
}

project(':util') {
    description = 'General utility functions'
    dependencies {
        compile 'org.ajoberstar:grgit:1.7.0'
        compile 'org.kohsuke:github-api:1.77'
        compile 'commons-validator:commons-validator:1.5.1'
    }
}
